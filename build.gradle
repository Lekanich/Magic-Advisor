plugins {
    id 'java'
    id 'org.jetbrains.intellij' version '0.4.18'
}

group 'lekanich'
version '1.0.3'

println "CI: ${System.getenv('CI')}"

// specified the last version
def ideaVersion = '2019.3'
if (project.hasProperty("intellijIdeaVersion")) {
    ideaVersion = intellijIdeaVersion ? intellijIdeaVersion : defaultIdeaVersion
}
println "IdeaVersion is : ${ideaVersion}"

def artifactVersion = ideaVersion ? version + "-" + ideaVersion : version
println "ArtifactVersion is : ${artifactVersion}"

version artifactVersion

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8
tasks.withType(JavaCompile) { options.encoding = 'UTF-8' }

repositories {
    mavenCentral()
}
dependencies {
    compileOnly group: 'org.projectlombok', name: 'lombok', version: '1.18.12'
    testCompile group: 'junit', name: 'junit', version: '4.13'
}

// See https://github.com/JetBrains/gradle-intellij-plugin/
intellij {
    version ideaVersion
    println "Building for version: ${version}"
}

buildPlugin {
    getArchiveClassifier().set("${intellij.version}")
}

if (project.hasProperty("intellijPublishToken")) {
    publishPlugin {
        token intellijPublishToken
    }
}

patchPluginXml {
    sinceBuild '2016.1'
    untilBuild '2019.3.4'
    changeNotes """
      Added your magic ball 8 advisor. If you have any questions - just ask!
      Added short instruction how to use it! =)
    """
}

runIde {
//  jvmArgs '-Didea.ProcessCanceledException=disabled'
}