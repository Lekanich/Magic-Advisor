plugins {
    id 'java'
    id 'org.jetbrains.intellij' version '0.6.4'
}

group 'lekanich'
String pluginVersion = '2020.2.02'

println "CI: ${System.getenv('CI')}"

// specified the last version
def ideaVersion = '2020.2'
if (project.hasProperty("intellijIdeaVersion")) {
    ideaVersion = project.property("intellijIdeaVersion")
}
println "IdeaVersion is : ${ideaVersion}"

String artifactVersion = ideaVersion ? pluginVersion + "-" + ideaVersion : pluginVersion
println "ArtifactVersion is : ${artifactVersion}"

version artifactVersion

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8
tasks.withType(JavaCompile) { options.encoding = 'UTF-8' }

repositories {
    mavenCentral()
}
dependencies {
    compileOnly group: 'org.projectlombok', name: 'lombok', version: '1.18.16'
    annotationProcessor 'org.projectlombok:lombok:1.18.16'

    testCompile group: 'junit', name: 'junit', version: '4.13.1'
}

// See https://github.com/JetBrains/gradle-intellij-plugin/
intellij {
    version ideaVersion
    println "Building for version: ${version}"
}

if (project.hasProperty("intellijPublishToken")) {
    publishPlugin {
        token intellijPublishToken
    }
}

patchPluginXml {
//    version pluginVersion
    sinceBuild '163'
    untilBuild '202.*'
    changeNotes """
     * [2020.2.01]<br/>
       Unify source code to support single version.<br/>
       Update plugin icon<br/>
     * [1.0.6]<br/>
      Added thoughts of Plato and Gandhi.<br/>
     * [1.0.5]<br/>
      Added ability to get wisdom from different philosophers.<br/>
      Added thoughts of Socrates.<br/>
     * [1.0.4]<br/>
      Added your magic ball 8 advisor. If you have any questions - just ask!<br/>
      Added short instruction how to use it! =)<br/>
    """
}

runIde {
//  jvmArgs '-Didea.ProcessCanceledException=disabled'
}

//curl -L --output verifier-all.jar https://dl.bintray.com/jetbrains/intellij-plugin-service/org/jetbrains/intellij/plugins/verifier-cli/<version>/verifier-cli-<version>-all.jar

//https://data.services.jetbrains.com/products?fields=code,name,releases.downloads,releases.version,releases.build,releases.type&code=DG,PS,DL,MPS,MPSIIP,RDCPPP,WS,AC,IIC,IIE,GO,CL,PCD,PCC,RD,PCE,IIU,RM,PCP
runPluginVerifier {
    ideVersions = "2016.3.8,2017.3.7,2018.3.6,2019.3.5,2020.1.4, 2020.2.3"
}